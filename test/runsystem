#!/usr/bin/perl

# Runs the system tests for multiple versions of NONMEM
# All arguments starting with "-" will be passed through to prove
# All other arguments are treated as nonmem versions to run
# For example: runsystem nm72 nm73 -j4

use strict;
use Config;
use includes;
use File::HomeDir;


my @prove_args;
my @nm_versions;

foreach my $arg (@ARGV) {
    if ($arg =~ /^-/) {
        push @prove_args, $arg;
    } else {
        push @nm_versions, $arg;
    }
}

if (scalar(@nm_versions) == 0) {
    push @nm_versions, "default";
}

# If the first nmversion is "all" get all versions from psn.conf
if ($nm_versions[0] eq "all") {
    @nm_versions = ();
	if (defined $PsN::config and (defined $PsN::config-> {'_'}) and
		defined ($PsN::config -> {'_'} -> {'PsN_test_all'})){
		my @tmp = split(',',$PsN::config -> {'_'} -> {'PsN_test_all'});
		foreach my $version (@tmp){
			if (defined $PsN::config->{nm_versions}->{$version}){
				push @nm_versions, $version;
			}
		}
	}else{
		foreach my $version (keys %{$PsN::config->{nm_versions}}) {
			if ($version ne "default") {
				push @nm_versions, $version;
			}
		}
	}
}

sub get_os_string{
	my $os='';

	if (defined $PsN::config and (defined $PsN::config-> {'_'}) and
		defined ($PsN::config -> {'_'} -> {'PsN_test_logfile_tag'})){
		#use tempdir from psn.conf instead of default
		$os = $PsN::config -> {'_'} -> {'PsN_test_logfile_tag'};
	}elsif (defined $Config{osname}){
		if ($Config{osname} eq 'MSWin32'){
			if (eval('require Win32')) { 
				$os = Win32::GetOSName();
			}
		}else{
			$os = $Config{osname};
		}
	}

	$os =~ s/\s+//g; #remove whitespace
	$os =~ s/\.//g; #dot
	$os =~ s/\///g; #forward slash
	if (length($os)>0){
		$os = '_'.$os;
	}
	return $os;
}

sub dirsep{
	if ((defined $Config{osname}) and ($Config{osname} eq 'MSWin32')){
		return '\\';
	}else{
		return '/';
	}
}

sub get_logfile{
	my $version = shift;
	my $dir = home();
	if (defined $PsN::config and (defined $PsN::config-> {'_'}) and
		defined ($PsN::config -> {'_'} -> {'PsN_test_logfile_dir'})){
		#use tempdir from psn.conf instead of default
		$dir = $PsN::config -> {'_'} -> {'PsN_test_logfile_dir'};
	}
	my $psn = $PsN::version;
	$psn =~ s/\./_/g;
	my $os = get_os_string;
	my $logfile = $dir.dirsep()."testlog".$os."_".$version."_psn_".$psn.".txt";
	return $logfile;
}

foreach my $nmversion (@nm_versions) {
	my $logfile = get_logfile($nmversion);
	if (-e $logfile){
		unlink($logfile);
	}
    print "=== Running system tests for NONMEM version: $nmversion ===\n";
    print "=== log file is $logfile\n";
    system("prove system -r @prove_args > ".'"'.$logfile.'"'." 2>&1 :: -nm_version=$nmversion "); #no -silent since redirect STDERR
}
